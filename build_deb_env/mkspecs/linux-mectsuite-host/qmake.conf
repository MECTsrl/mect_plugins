#
# qmake configuration for MectSuite Host Apps (target = host)
#

# host operating system == device operating system
contains(QMAKE_HOST.os,"Windows") {
	MECT_PREFIX        = C:/Qt487
	CROSS_COMPILE      =
	MAKEFILE_GENERATOR = MINGW
	TARGET_PLATFORM	   = win32
} else : contains(QMAKE_HOST.os,"Linux") {
	MECT_PREFIX        = /opt/Qt4.8.7
	CROSS_COMPILE      =
	MAKEFILE_GENERATOR = UNIX
	TARGET_PLATFORM	   = unix
} else {
	error(unknown host operating system $${QMAKE_HOST.os});
}
TEMPLATE		= app
CONFIG			+= qt warn_on release incremental link_prl gdb_dwarf_index
QT			+= core gui network
QMAKE_INCREMENTAL_STYLE = sublib

include(../common/linux.conf)
include(../common/gcc-base-unix.conf)
include(../common/g++-unix.conf)

# NB: -Wno-psabi avoids qstring.h warning "note: the mangling of 'va_list' has changed in GCC 4.4"
QMAKE_CXXFLAGS += -std=gnu++98 -Wno-error=class-memaccess -Wno-psabi

load(qt_config)

exists(/usr/include/glib-2.0) : exists(/usr/include/glib-2.0/glib) : exists(/usr/lib/x86_64-linux-gnu/glib-2.0/include) : exists(/usr/lib/x86_64-linux-gnu/glib-2.0) {
	QMAKE_INCDIR += /usr/include/glib-2.0 /usr/include/glib-2.0/glib /usr/lib/x86_64-linux-gnu/glib-2.0/include
	QMAKE_LIBDIR += /usr/lib/x86_64-linux-gnu/glib-2.0
	QMAKE_LIBS   += -lglib-2.0 -lgthread-2.0
}

exists( $$[QT_INSTALL_PREFIX]/ssl/include ) : exists( $$[QT_INSTALL_PREFIX]/ssl/lib ) {
	QMAKE_INCDIR += $$[QT_INSTALL_PREFIX]/ssl/include
	QMAKE_LIBDIR += $$[QT_INSTALL_PREFIX]/ssl/lib
}

exists( $$[QT_INSTALL_HEADERS]/Qwt ) : exists( $$[QT_INSTALL_PREFIX]/lib/libqwt.so ) {
	QMAKE_INCDIR_QT = $$[QT_INSTALL_HEADERS]/Qwt $${QMAKE_INCDIR_QT}
	QMAKE_LIBDIR   += $$[QT_INSTALL_PREFIX]/lib
	QMAKE_LIBS     += -lqwt 
}

exists( $$[QT_INSTALL_PREFIX]/usr/include ) : exists( $$[QT_INSTALL_PREFIX]/usr/lib ) {
	QMAKE_INCDIR += $$[QT_INSTALL_PREFIX]/usr/include
	QMAKE_LIBDIR += $$[QT_INSTALL_PREFIX]/usr/lib
}

QT_LRELEASE_PATH = $${MECT_PREFIX}/desktop/bin
QT_LUPDATE_PATH  = $${MECT_PREFIX}/desktop/bin

ATCM_TEMPLATE_BASE_DIR = $${MECT_PREFIX}/desktop/QtCreator/share/qtcreator/templates/wizards

# C compiler
QMAKE_CFLAGS           += -D_REENTRANT
QMAKE_CFLAGS_RELEASE   += -O3
QMAKE_CFLAGS_DEBUG     += -O2 -g # NB: -O2 mandatory

# C++ compiler
QMAKE_CXXFLAGS         += -D_REENTRANT
QMAKE_CXXFLAGS_RELEASE += -O3
QMAKE_CXXFLAGS_DEBUG   += -O2 -g # NB: -O2 mandatory

# linker libraries
QMAKE_LIBS             += -lrt -lpthread -ldl
QMAKE_LIBS             += -lpng -lz

# linker flags
QMAKE_LFLAGS           += $${QMAKE_LFLAGS_RPATH}$$[QT_INSTALL_PREFIX]/lib
QMAKE_LFLAGS           += $${QMAKE_LFLAGS_RPATH}$$[QT_INSTALL_PREFIX]/usr/lib
QMAKE_LFLAGS_RELEASE   += -Wl,-O1
QMAKE_LFLAGS_DEBUG     += -Wl,-O1 -g

