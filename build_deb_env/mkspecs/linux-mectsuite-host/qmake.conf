#
# qmake configuration for building with arm-none-linux-gnueabi-g++
#
include(../common/gcc-base-unix.conf)
include(../common/g++-unix.conf)
include(../common/linux.conf)
include(../common/qws.conf)

# host operating system
contains(QMAKE_HOST.os,"Windows"){
	MECT_PREFIX        = C:/Qt487
	CROSS_COMPILE      =
}
contains(QMAKE_HOST.os,"Linux"){
	MECT_PREFIX        = /opt/Qt4.8.7
	CROSS_COMPILE      =
}

# device operating system
win32 {
	MAKEFILE_GENERATOR = MINGW
}
unix {
	MAKEFILE_GENERATOR = UNIX
}

CONFIG                 += qt warn_on release incremental link_prl

# NB: -Wno-psabi avoids qstring.h warning "note: the mangling of 'va_list' has changed in GCC 4.4"
DEVICE_CFLAGS = -O2 -Wno-psabi -DTARGET_ARM

# NB: there is no "/opt/Qt4.8.7/host/rootfs" extra folder
QT_ROOTFS = $${MECT_PREFIX}/host

QT_LRELEASE_PATH = $${MECT_PREFIX}/desktop/bin
QT_LUPDATE_PATH  = $${MECT_PREFIX}/desktop/bin

ATCM_TEMPLATE_BASE_DIR = $${MECT_PREFIX}/desktop/QtCreator/share/qtcreator/templates/wizards

# modifications to g++.conf
QMAKE_CC                = $${CROSS_COMPILE}gcc
QMAKE_CXX               = $${CROSS_COMPILE}g++
QMAKE_LINK              = $${CROSS_COMPILE}g++
QMAKE_LINK_SHLIB        = $${CROSS_COMPILE}g++

QMAKE_AR                = $${CROSS_COMPILE}ar cqs
QMAKE_OBJCOPY           = $${CROSS_COMPILE}objcopy
QMAKE_STRIP             = $${CROSS_COMPILE}strip
QMAKE_RANLIB            = $${CROSS_COMPILE}ranlib

# run cc ?

# include
QMAKE_INCDIR            = $${MECT_PREFIX}/host/include $${MECT_PREFIX}/host/usr/include \
	/usr/include/glib-2.0 /usr/include/glib-2.0/glib /usr/lib/x86_64-linux-gnu/glib-2.0/include # configure openssl, mysql, glib
QMAKE_INCDIR_QT         = $${QMAKE_INCDIR} $${QT_ROOTFS_KERNEL_INC} $$[QT_INSTALL_HEADERS]

QMAKE_PCH_OUTPUT_EXT    = .gch

# libraries
QMAKE_LIBDIR            = $${MECT_PREFIX}/host/lib $${MECT_PREFIX}/host/usr/lib # configure openssl, mysql, glib
QMAKE_LIBDIR_QT         = $${QMAKE_LIBDIR} $$[QT_INSTALL_LIBS]

QMAKE_MOC               = $$[QT_INSTALL_BINS]/moc
QMAKE_UIC               = $$[QT_INSTALL_BINS]/uic

QMAKE_COPY              = cp -f
QMAKE_COPY_FILE         = $(COPY)
QMAKE_COPY_DIR          = $(COPY) -r
QMAKE_MOVE              = mv -f
QMAKE_DEL_FILE          = rm -f
QMAKE_MKDIR             = mkdir -p
QMAKE_DEL_DIR           = rmdir
QMAKE_CHK_DIR_EXISTS    = test -d

QMAKE_TAR               = tar -cf
QMAKE_GZIP              = gzip -9f

QMAKE_INSTALL_FILE      = install -m 644 -p
QMAKE_INSTALL_PROGRAM   = install -m 755 -p

TEMPLATE                = app
QT                     += core gui
QMAKE_INCREMENTAL_STYLE = sublib

#modifications to gcc-base.conf

# C compiler
QMAKE_CFLAGS           += $$DEVICE_CFLAGS -D_REENTRANT
QMAKE_CFLAGS_RELEASE   += -O3
QMAKE_CFLAGS_DEBUG     += -g # NB: -O2 already present and mandatory

# C++ compiler
QMAKE_CXXFLAGS         += $$QMAKE_CFLAGS
QMAKE_CXXFLAGS_RELEASE += $$QMAKE_CFLAGS_RELEASE

# linker command
QMAKE_LINK_C            = $${CROSSCOMPILE}gcc
QMAKE_LINK_C_SHLIB      = $${CROSSCOMPILE}gcc

# linker libraries
QMAKE_LIBS             += -lrt -lpthread -ldl
QMAKE_LIBS             += -lpng -lz
QMAKE_LIBS             += -lglib-2.0 -lgthread-2.0

# linker flags
QMAKE_LFLAGS           += $${QMAKE_LFLAGS_RPATH}$${MECT_PREFIX}/host/lib 
QMAKE_LFLAGS           += $${QMAKE_LFLAGS_RPATH}$${MECT_PREFIX}/host/usr/lib 
QMAKE_LFLAGS_RELEASE   += -Wl,-O1
QMAKE_LFLAGS_DEBUG     += -g

load(qt_config)
