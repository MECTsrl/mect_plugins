#
# qmake configuration for linux-g++
#

MAKEFILE_GENERATOR	= UNIX
TARGET_PLATFORM		= unix
TEMPLATE		= app
CONFIG			+= qt warn_on release incremental link_prl gdb_dwarf_index
QT			+= core gui
QMAKE_INCREMENTAL_STYLE = sublib

include(../common/linux.conf)
include(../common/gcc-base-unix.conf)
include(../common/g++-unix.conf)

QMAKE_CXXFLAGS = $$QMAKE_CFLAGS -std=gnu++98 -Wno-error=class-memaccess

load(qt_config)

exists( $$[QT_INSTALL_PREFIX]/ssl/include ) : exists( $$[QT_INSTALL_PREFIX]/ssl/lib ) {
	QMAKE_INCDIR += $$[QT_INSTALL_PREFIX]/ssl/include
	QMAKE_LIBDIR += $$[QT_INSTALL_PREFIX]/ssl/lib
}

exists( $$[QT_INSTALL_HEADERS]/Qwt ) : exists( $$[QT_INSTALL_PREFIX]/lib/libqwt.so ) {
	QMAKE_INCDIR_QT = $$[QT_INSTALL_HEADERS]/Qwt $${QMAKE_INCDIR_QT}
	QMAKE_LIBDIR   += $$[QT_INSTALL_PREFIX]/lib
	QMAKE_LIBS     += -lqwt 
}

